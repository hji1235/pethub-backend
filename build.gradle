plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.6'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'org.asciidoctor.jvm.convert' version '3.3.2' // asciidoctor 플러그인 추가
}

group = 'com.hji1235'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	asciidoctorExt // asciidoctorExt로 라이브러리 불러오기
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets")) // snippetDir 변수 설정
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// restdocs 라이브러리 추가
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('testClasses') {
	doFirst { // 이전에 생성한 HTML 파일 삭제
		delete file('build/docs/asciidoc')
	}
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
	finalizedBy asciidoctor // test 종료 시 asciidoctor 작업 실행
}

tasks.named('asciidoctor') {
	dependsOn test
	configurations 'asciidoctorExt'
	inputs.dir snippetsDir
	finalizedBy copyDocument // asciidoctor 작업 종료 시 copyDocument 수행
	doFirst { // asciidoctor 작업 수행 전 이전 문서 삭제
		delete file('src/main/resources/static/docs')
	}
}

task copyDocument(type: Copy) { // asciidoctor 작업 결과물 복사
	dependsOn asciidoctor
	from file('build/docs/asciidoc')
	into file('src/main/resources/static/docs')
}

bootJar {
	dependsOn asciidoctor
	doFirst {
		delete file('static/docs')
	}
	from("${asciidoctor.outputDir}") {
		into 'static/docs'
	}
}

build {
	finalizedBy copyDocument
}
